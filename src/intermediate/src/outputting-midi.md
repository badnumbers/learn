# Outputting MIDI

## Creating a connection to the MIDI port

SuperCollider provides a class called `MIDIOut` to handle outputting MIDI to destinations outside SuperCollider. Before using `MIDIOut`, you will first need to call the `init` method of `MIDIClient`:

```
MIDIClient.init;
```

This will output to the post window a list of MIDI devices such as the following:

```
MIDI Sources:
	MIDIEndPoint("System", "Timer")
	MIDIEndPoint("System", "Announce")
	MIDIEndPoint("Midi Through", "Midi Through Port-0")
	MIDIEndPoint("Scarlett 6i6 USB", "Scarlett 6i6 USB MIDI 1")
	MIDIEndPoint("SuperCollider", "out0")
	MIDIEndPoint("SuperCollider", "out1")
MIDI Destinations:
	MIDIEndPoint("Midi Through", "Midi Through Port-0")
	MIDIEndPoint("Scarlett 6i6 USB", "Scarlett 6i6 USB MIDI 1")
	MIDIEndPoint("SuperCollider", "in0")
	MIDIEndPoint("SuperCollider", "in1")
```

I want to sound MIDI data out through my Scarlett audio interface, so I create a `MIDIOut` corresponding to one of those devices by copying and pasting the appropriate device names into a call to `MIDIOut.newByName()`:

```
~midiOut = MIDIOut.newByName("Scarlett 6i6 USB", "Scarlett 6i6 USB MIDI 1");
```

`MIDIOut` also has a property called `latency` which adds latency before sending MIDI messages, in order to match SuperCollider's internal latency. It's advisable to set this to the server's latency, and can be done at the time of creating the `MIDIOut`:

```
~midiOut = MIDIOut.newByName("Scarlett 6i6 USB", "Scarlett 6i6 USB MIDI 1").latency_(s.latency);
```

Or later if you prefer:

```
~midiOut.latency = s.latency;
```

You may occasionally find you need to tweak the latency value of your `MIDIOut` in order to get it to exactly match synths in SuperCollider. You can set the value at any time, including when a pattern is playing MIDI data.

## Sending data from the `MIDIOut`

The class can be used in two different ways:
1. Calling methods on `MIDIOut` directly. This is a more direct approach.
1. Using `MIDIOut` in conjunction with patterns, involving a special type of `Event` called `\midi`.

We will initially examine the second way of working with `MIDIOut`, since it is common to write musical sequences in SuperCollider using patterns.

### Sending MIDI data by using patterns

Using patterns to control MIDI data is almost identical to using patterns to control SuperCollider SynthDefs. The event type you need to use is `\midi`. A pattern which sends MIDI data uses some special keys:

* `\midiout`: The `MIDIOut` instance you want to send MIDI data through (see above).
* `\midicmd`: The type of MIDI message you want to send. The default is `\noteOn`.
* `\chan`: The MIDI channel number. SuperCollider thinks of these as 0-based, so the MIDI channel numbers in your patterns should be between 0 (for MIDI channel 1) and 15 (for MIDI channel 16).

Here is an example:

```
(
Pdef(\myPattern,
	Pbind(
		\type, \midi,
		\midiout, ~midiOut,
		\chan, Pseq([0],1)
		\midicmd, \noteOn,
		\dur, 0.25,
		\legato, 0.5,
		\degree, Pseq({7.rand2}!16,inf),
		\octave, 4,
		\amp, Pseq([1,0.5],inf)
	)
).play;
)
```

As well as specifying MIDI note numbers directly using the key `\midinote`, you can also use `\note` or `\degree` and these will be converted to MIDI note numbers in the same way that they would be converted to frequency values in a pattern written to play a SynthDef. And of course, `\degree` can be used in conjunction with `\scale`, although due to the nature of MIDI itself, only the  [12-TET](https://en.wikipedia.org/wiki/Equal_temperament#12TET) tuning is supported.

The `\amp` key is converted to a note on velocity of between 0 and 127.

The pattern above, although its `midicmd` is `noteOn`, will also send MIDI note off messages. It will send one at the end of each note generated by the pattern, so if an event generated by the pattern has a `\dur` of 0.5 and a `\legato` of 0.5, then the MIDI note off will be sent 0.25 beats after the MIDI note on.

This behaviour is usually what you want. If however you wish to send only MIDI note on messages, without MIDI note off messages (for example to play and hold down a chord as input to an arpeggiator), then also specify the `\hasGate` key with a value of `false`:

```
(
Pdef(\playChord,
	Pbind(
		\type, \midi,
		\midiout, ~midiOut,
		\chan, Pseq([0],1)
		\midicmd, \noteOn,
		\hasGate, false,
		\degree, Pseq([0,2,4],1),
		\octave, 4
	)
).play;
)
```

#### Other MIDI commands

The following MIDI commands are also supported:

##### `\noteOff`
Used to send MIDI note off messages when you previously sent note on messages with the key `\hasGate` equal to `false`.

```
(
Pdef(\stopChord,
	Pbind(
		\type, \midi,
		\midiout, ~midiOut,
        \chan, Pseq([0],1)
		\midicmd, \noteOff,
		\degree, Pseq([0,2,4],1),
		\octave, 4
	)
).play;
)
```
The `noteOff` MIDI command also supports the `\amp` key, which will be converted to [release velocity](http://electronicmusic.wikia.com/wiki/Release_velocity).

##### `\allNotesOff`

```
(
Pdef(\stopAllNotes,
	Pbind(
		\type, \midi,
		\midiout, ~midiOut,
        \chan, Pseq([0],1)
		\midicmd, \allNotesOff,
	)
).play;
)
```
This turns off all notes for the MIDI channel in case some of them are stuck. Note that above I have wrapped the value for the `\chan` key in a `Pseq` with 1 repeat. This is because otherwise the pattern will send 'all note off' events continually until stopped, which may cause some confusion!

##### `\control`
```
(
Pdef(\wiggleCutoff,
	Pbind(
		\type, \midi,
		\midiout, ~midiOut,
        \chan, 0
		\midicmd, \control,
		\ctlNum, 74, // Typically filter cutoff
		\control, Pseq([0,64,127],inf),
	)
).play;
)
```
Sends controller values. For the `control` MIDI command, the `\ctlNum` key specifies the controller number and the `\control` key specifies the actual value you want to send for that controller. Controller numbers and values both range between 0 and 127. Note that although SuperCollider thinks of numbers as being zero-based, controller numbers are also zero-based so there's no need to adjust them down by one. 

For a nice continuous modulation, I recommend `Pseg`, which is great for describing smooth value changes, in conjunction with a very short `\dur`, which will send very frequent controller value updates. Here is an example which uses `Ppar` to combine sending note values (in this case just a repeating note) at the same time as the controller messages:
```

```
##### `\bend`
```
(
Pdef(\bendy,
	Pbind(
		\type, \midi,
		\midiout, ~midiOut,
        \chan, 0,
		\midicmd, \bend,
		\val, Pseq([0,16383],inf) // alternate between minimum and maximum
	)
).play;
)
```
Sends pitch bend. 0 is the minimum, 16383 the maximum and 8192 is centered (as described [here](http://sites.uci.edu/camp2014/2014/04/30/managing-midi-pitchbend-messages/)). The actual range of the pitch bending you get depends on your synthesizer's configuration: typically the whole range covers two semitones or an octave in each direction.

##### `\touch`
```
(
Pdef(\touch,
	Pbind(
		\type, \midi,
		\midiout, ~midiOut,
        \chan, 0,
		\midicmd, \touch,
		\val, Pseq([0,127],inf) // alternate between minimum and maximum
	)
).play;
)
```
Sends [aftertouch](https://www.sweetwater.com/insync/aftertouch/) data, i.e. data about pressure on the keyboard's key while the note is still being held. This applies to all notes being played.

##### `\polyTouch`
```
(
Pdef(\touch,
	Pbind(
		\type, \midi,
		\midiout, ~midiOut,
        \chan, 0,
		\midicmd, \touch,
		\midinote, 64,
		\polyTouch, Pseq([0,127],inf) // alternate between minimum and maximum
	)
).play;
)
```
Sends [aftertouch](https://www.sweetwater.com/insync/aftertouch/) data which is specific to a particular note, hence the use of the `\midinote` key to indicate which note the aftertouch is being applied to. As with `\noteOn`, the `\midinote` key can be specified as `\degree` or `\note` instead.
##### `\program`
```
(
Pdef(\programChange,
	Pbind(
		\type, \midi,
		\midiout, ~midiOut,
        \chan, 0,
		\midicmd, \program,
		\progNum, 14
	)
).play;
)
```
Sends a program (patch) change message. The `\progNum` values range from 0 to 127.



